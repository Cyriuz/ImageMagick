# Get the platform

if(WIN32)
  if(NOT WINDOWS)
    set(WINDOWS TRUE)
  endif()
  # TODO check for mingw or cygwin ???
elseif(UNIX AND NOT APPLE)
  if(CMAKE_SYSTEM_NAME MATCHES ".*Linux")
    set(LINUX TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "kFreeBSD.*")
    set(FREEBSD TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "kNetBSD.*|NetBSD.*")
    set(NETBSD TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "kOpenBSD.*|OpenBSD.*")
    set(OPENBSD TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES ".*GNU.*")
    set(GNU TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES ".*BSDI.*")
    set(BSDI TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "DragonFly.*|FreeBSD")
    set(FREEBSD TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "SYSV5.*")
    set(SYSV5 TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "Solaris.*")
    set(SOLARIS TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "HP-UX.*")
    set(HPUX TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "AIX.*")
    set(AIX TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES "Minix.*")
    set(MINIX TRUE)
  endif()
elseif(APPLE)
  if(CMAKE_SYSTEM_NAME MATCHES ".*Darwin.*")
    set(DARWIN TRUE)
  elseif(CMAKE_SYSTEM_NAME MATCHES ".*MacOS.*")
    set(MACOSX TRUE)
  endif()
  # TODO: iOS?
elseif(CMAKE_SYSTEM_NAME MATCHES "BeOS.*")
  set(BEOS TRUE)
elseif(CMAKE_SYSTEM_NAME MATCHES "Haiku.*")
  set(HAIKU TRUE)
endif()

# if we're on MacOS X we enable the universal build
if(APPLE)
  set(AC_APPLE_UNIVERSAL_BUILD TRUE)
endif()

find_program(UNAME NAMES uname)
if(UNAME)
  execute_process(COMMAND "${UNAME}" -m OUTPUT_VARIABLE host_cpu OUTPUT_STRIP_TRAILING_WHITESPACE)
  execute_process(COMMAND "${UNAME}" -r OUTPUT_VARIABLE host_os OUTPUT_STRIP_TRAILING_WHITESPACE)
  execute_process(COMMAND "${UNAME}" -s OUTPUT_VARIABLE host_vendor OUTPUT_STRIP_TRAILING_WHITESPACE)
else()
  # These definitely do not match the uname info used by autotools, but it's better than nothing
  set(host_cpu ${CMAKE_HOST_SYSTEM_PROCESSOR})
  set(host_os ${CMAKE_HOST_SYSTEM_NAME})
  set(host_vendor ${CMAKE_HOST_SYSTEM_NAME})
endif()
if(NOT CMAKE_CROSSCOMPILING)
  set(MAGICK_TARGET_CPU ${host_cpu})
  set(MAGICK_TARGET_OS ${host_os})
  set(MAGICK_TARGET_VENDOR ${host_vendor})
else()
  set(MAGICK_TARGET_CPU ${CMAKE_SYSTEM_PROCESSOR})
  set(MAGICK_TARGET_OS ${CMAKE_SYSTEM_NAME})
  set(MAGICK_TARGET_VENDOR ${CMAKE_SYSTEM_NAME})
endif()
set(host "${host_vendor}-${host_os}-${host_cpu}")
set(target "${MAGICK_TARGET_VENDOR}-${MAGICK_TARGET_OS}-${MAGICK_TARGET_CPU}")
